apply plugin: 'com.android.application'

def gitCommitShortHash = 'git log -1 --pretty=%h'.execute([], project.rootDir).text.trim()
def gitCommitDate = 'git log -1 --pretty=%ci'.execute([], project.rootDir).text.trim().split()[0]
def gitShortShaDate = gitCommitShortHash + "_" + gitCommitDate

// Auto-incrementing commit count based on counting commits to HEAD (Build #543)
def gitCommitCount = Integer.parseInt('git rev-list HEAD --count'.execute([], project.rootDir).text.trim())

// I want to use git tags as my version names (1.2.2)
def gitCurrentTag = 'git describe --tags --abbrev=0'.execute([], project.rootDir).text.trim()

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.1'
    defaultConfig {
        applicationId "cn.edu.siso.rlxapf"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode gitCommitCount
        versionName gitCurrentTag
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        releaseConfig {
            storeFile file("../keys/LX.jks")
            storePassword project.hasProperty("KEYSTORE_PASS") ? KEYSTORE_PASS : System.getenv("KEYSTORE_PASS")
            keyAlias project.hasProperty("ALIAS_NAME") ? ALIAS_NAME : System.getenv("ALIAS_NAME")
            keyPassword project.hasProperty("ALIAS_PASS") ? ALIAS_PASS : System.getenv("ALIAS_PASS")
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation files('libs/usr_cloud_com.jar')
    implementation files('libs/usrcloud.jar')
    implementation 'com.android.support:appcompat-v7:27.0.1'
    implementation 'com.android.support:support-v4:27.0.1'
    implementation 'com.android.support:design:27.0.1'
    implementation 'com.android.support:support-vector-drawable:27.0.1'
    implementation 'com.android.support:preference-v7:27.0.1'
    implementation 'com.android.support:preference-v14:27.0.1'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.truizlop.sectionedrecyclerview:library:1.2.0'
    implementation 'com.github.lecho:hellocharts-library:1.5.8@aar'
    implementation 'de.hdodenhof:circleimageview:2.1.0'
    implementation 'com.alibaba:fastjson:1.2.40'
    implementation 'com.squareup.okhttp3:okhttp:3.8.1'
    implementation 'com.github.sahasbhop:apng-view:1.3'
    implementation 'org.apache.commons:commons-lang3:3.6'
    implementation 'com.tencent.bugly:crashreport:2.6.6.1'
    implementation 'eu.davidea:flexible-adapter:5.0.0-rc3'
    implementation 'eu.davidea:flexible-adapter-livedata:1.0.0-b1'
    implementation 'eu.davidea:flexible-adapter-databinding:1.0.0-b2'
    implementation 'eu.davidea:flexible-adapter:5.0.0-SNAPSHOT'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
